plugins {
    id 'java'
}

group 'com.chaos'
version '1.4-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java.srcDirs = ['src/main']
        resources {
            srcDirs 'src/resources'
            includes['**/*.txt']
        }
    }
}

configurations {
    // configuration that holds jars to include in the jar
    extraLibs
}

dependencies {
    extraLibs 'org.ow2.asm:asm:8.0.1'
    extraLibs 'org.ow2.asm:asm-commons:8.0.1'
    configurations.implementation.extendsFrom(configurations.extraLibs)
}

jar {
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes(
                'Main-Class': 'com.tonicsystems.jarjar.Main'
        )
    }
    doLast {
        def lib = outputs.files.singleFile
        def lastLib = new File(lib.parentFile, "${lib.name.take(lib.name.lastIndexOf('.'))}-processed.jar")
        exec {
            workingDir "${lib.parentFile.absolutePath}"
            executable 'java'
            args '-jar', "${lib.name}", "process", "${project.file('jarjar-rules.txt').absolutePath}",
                    "${lib.name}", "${lastLib.name}"
        }
        lastLib.renameTo(lib)
    }
}